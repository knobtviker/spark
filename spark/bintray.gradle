apply plugin: 'com.jfrog.bintray'

version = android.defaultConfig.versionName

if (project.hasProperty("android")) { // Android libraries
    task sourcesJar(type: Jar) {
        classifier = 'sources'
        from android.sourceSets.main.java.srcDirs
    }

    task javadoc(type: Javadoc) {
        failOnError false
        options.addBooleanOption('Xdoclint:none', true)
        source = android.sourceSets.main.java.srcDirs
        classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
    }
} else { // Java libraries
    task sourcesJar(type: Jar, dependsOn: classes) {
        classifier = 'sources'
        from sourceSets.main.allSource
    }
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives javadocJar
    archives sourcesJar
}

// Bintray
Properties properties = new Properties()
properties.load(new FileInputStream(file(rootProject.file('bintray.properties'))))

bintray {
    user = properties.getProperty("bintray.user")
    key = properties.getProperty("bintray.apikey")

    pkg {
        repo = 'maven'
        name = 'spark'
        desc = 'Spark is a simple Android library that takes a series of x,y points at any scale and draws them as a sparkline chart.'
        websiteUrl = 'https://github.com/knobtviker/spark'
        issueTrackerUrl = 'https://github.com/knobtviker/spark/issues'
        vcsUrl = 'https://github.com/knobtviker/spark.git'
        licenses = ['MIT']
        version {
            name = android.defaultConfig.versionName
            gpg {
                sign = true
                passphrase = properties.getProperty("bintray.gpg_passphrase")
            }
        }
        publish = true
        override = true
        publicDownloadNumbers = true
    }

    configurations = ['archives']
}